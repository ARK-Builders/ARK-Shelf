plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "space.taran.arkshelf"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"
        setProperty("archivesBaseName", "ark-shelf")
        def login = System.getenv("ACRA_LOGIN") ?: ""
        def password = System.getenv("ACRA_PASS") ?: ""
        def uri = System.getenv("ACRA_URI") ?: ""
        buildConfigField "String", "ACRA_LOGIN", "\"$login\""
        buildConfigField "String", "ACRA_PASS", "\"$password\""
        buildConfigField "String", "ACRA_URI", "\"$uri\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        testRelease {
            storeFile project.rootProject.file('./testRelease.jks')
            storePassword "arkshelf"
            keyAlias "key0"
            keyPassword "arkshelf"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher_1",
                    appLabel: "@string/app_name_debug"
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.testRelease
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher_2",
                    appLabel: "@string/app_name"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        freeCompilerArgs=['-Xjvm-default=all']
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }
}

dependencies {
    implementation "space.taran:arklib:0.1.0-SNAPSHOT-f81e34a657"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.fragment:fragment-ktx:1.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation 'com.github.bumptech.glide:glide:4.13.0'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation "androidx.paging:paging-runtime-ktx:3.1.1"

    implementation "com.google.dagger:dagger:2.41"
    kapt "com.google.dagger:dagger-compiler:2.41"

    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.3'

    implementation "org.orbit-mvi:orbit-viewmodel:4.3.2"
    implementation "com.ericktijerou.koleton:koleton:1.0.0-beta01"

    implementation "ch.acra:acra-http:5.9.3"
    implementation "ch.acra:acra-dialog:5.9.3"

    implementation 'com.github.ARK-Builders:ark-filepicker:c6d66141c1'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
